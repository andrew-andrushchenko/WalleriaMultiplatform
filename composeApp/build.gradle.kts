import com.android.build.gradle.internal.cxx.configure.gradleLocalProperties
import org.jetbrains.compose.desktop.application.dsl.TargetFormat
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.composeMultiplatform)
    alias(libs.plugins.composeCompiler)
    alias(libs.plugins.composeHotReload)
    alias(libs.plugins.kotlinSerialization)
    alias(libs.plugins.kotlinParcelize)
    alias(libs.plugins.ksp)
    alias(libs.plugins.room)
}

val generateBuildConfig by tasks.registering(Sync::class) {
    val accessKey = gradleLocalProperties(rootDir, providers).getProperty("unsplash_access_key_debug")
    val secretKey = gradleLocalProperties(rootDir, providers).getProperty("unsplash_secret_key_debug")
    val pageSize = gradleLocalProperties(rootDir, providers).getProperty("pagination_page_size")

    from(
        resources.text.fromString("""
        |package com.andrii_a.walleria
        |
        |object BuildConfig {
        |    const val UNSPLASH_ACCESS_KEY = "$accessKey"
        |    const val UNSPLASH_SECRET_KEY = "$secretKey"
        |    const val PAGINATION_PAGE_SIZE= "$pageSize"
        |}
        |
      """.trimMargin()
        )
    ) {
        rename { "BuildConfig.kt" } // set the file name
        into("com/andrii_a/walleria/") // change the directory to match the package
    }

    into(layout.buildDirectory.dir("generated-src/"))
}

kotlin {
    androidTarget {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_11)
            //freeCompilerArgs.addAll("-P", "plugin:org.jetbrains.kotlin.parcelize:additionalAnnotation=com.andrii_a.walleria.domain.util.PlatformParcelize")
        }
    }
    
    listOf(
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "ComposeApp"
            isStatic = true
        }
    }
    
    jvm()
    
    sourceSets {
        androidMain.dependencies {
            implementation(compose.preview)
            implementation(libs.androidx.activity.compose)

            implementation(libs.ktor.client.okhttp)
            implementation(libs.koin.android)
            implementation(libs.koin.androidx.compose)

            implementation(libs.room.runtime.android)

            implementation(libs.androidx.core.splashscreen)
            implementation(libs.androidx.browser)
        }
        commonMain {
            kotlin.srcDir(generateBuildConfig.map { it.destinationDir })
        }
        commonMain.dependencies {
            implementation(compose.runtime)
            implementation(compose.foundation)
            //implementation(compose.material3)
            implementation(libs.material3expressive)
            implementation(compose.ui)
            implementation(compose.uiUtil)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)
            implementation(libs.androidx.lifecycle.viewmodelCompose)
            implementation(libs.androidx.lifecycle.runtimeCompose)
            implementation(compose.material3AdaptiveNavigationSuite)
            implementation(compose.materialIconsExtended)
            implementation(compose.components.resources)
            implementation(compose.components.uiToolingPreview)

            implementation(libs.bundles.ktor)

            api(libs.koin.core)
            implementation(libs.koin.compose)
            implementation(libs.koin.compose.viewmodel)
            implementation(libs.navigation.compose)

            implementation(libs.coil)
            implementation(libs.coil.compose.core)
            implementation(libs.coil.compose)
            implementation(libs.coil.network.ktor)

            implementation(libs.kotlinx.serialization.json)

            implementation(libs.constraintlayout)

            implementation(libs.kotlinx.datetime)

            implementation(libs.material3.adaptive)
            implementation(libs.material3.adaptive.layout)
            implementation(libs.material3.adaptive.navigation)

            implementation(libs.room.runtime)
            implementation(libs.sqlite.bundled)

            implementation(libs.paging.common)
            implementation(libs.paging.compose)

            implementation(libs.constraintlayout)

            implementation(libs.datastore)
            implementation(libs.datastore.preferences)

            implementation(libs.cmptoast)
        }
        commonTest.dependencies {
            implementation(libs.kotlin.test)
            implementation(libs.paging.common)
        }
        jvmMain.dependencies {
            implementation(compose.desktop.currentOs)
            implementation(libs.kotlinx.coroutinesSwing)

            implementation(libs.ktor.client.okhttp)
            implementation(libs.ktor.server.core)
            implementation(libs.ktor.server.netty)

            implementation(libs.filekit.core)
            implementation(libs.filekit.dialogs)
            implementation(libs.filekit.dialogs.compose)
            implementation(libs.filekit.coil)
        }
    }
}

android {
    namespace = "com.andrii_a.walleria"
    compileSdk = libs.versions.android.compileSdk.get().toInt()

    defaultConfig {
        applicationId = "com.andrii_a.walleria"
        minSdk = libs.versions.android.minSdk.get().toInt()
        targetSdk = libs.versions.android.targetSdk.get().toInt()
        versionCode = 1
        versionName = "1.0"
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
    buildTypes {
        getByName("release") {
            isMinifyEnabled = false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
}

dependencies {
    listOf(
        "kspAndroid",
        //"kspIosX64",
        "kspJvm"
    ).forEach {
        add(it, libs.room.compiler)
    }
    debugImplementation(compose.uiTooling)
}

room {
    schemaDirectory("$projectDir/schemas")
}

compose.desktop {
    application {
        mainClass = "com.andrii_a.walleria.MainKt"

        nativeDistributions {
            targetFormats(TargetFormat.Dmg, TargetFormat.Msi, TargetFormat.Deb)
            packageName = "com.andrii_a.walleria"
            packageVersion = "1.0.0"
        }
    }
}
